/*

    Minimum Profit - A Text Editor
    Hexadecimal viewer.

    ttcdt <dev@triptico.com> et al.

    This software is released into the public domain.
    NO WARRANTY. See file LICENSE for details.

*/

/** hexadecimal viewer object **/

global mp_hex_view = new(mp_base, {
        name:           '<hex view>',
        offset:         0,
        tx:             0,
        bpl:            8,
        data:           '',
        f_offset:       0,
        f_size:         0,
        fd:             NULL,
        h_cursor:       0,

        get_status: sub (hex) {
            sprintf("%s - 0x%06x (%d)", name, offset + h_cursor, offset + h_cursor);
        },
        init:       sub (hex) {
            if ((fd = open(name, "rb")) == NULL)
                hex = NULL;

            name = sprintf(L("<hex view of %s>"), name);

            return hex;
        },
        keypress:   sub (hex, keycode) {
            if (keycode == 'cursor-down')
                offset += bpl;
            else
            if (keycode == 'cursor-up')
                offset -= bpl;
            else
            if (keycode == 'cursor-left' && h_cursor > 0)
                h_cursor -= 1;
            else
            if (keycode == 'cursor-right' && h_cursor < bpl - 1)
                h_cursor += 1;
            else
            if (keycode == 'page-down' || keycode == 'mouse-wheel-down')
                offset += bpl * (mp.window.ty - 1);
            else
            if (keycode == 'page-up' || keycode == 'mouse-wheel-up')
                offset -= bpl * (mp.window.ty - 1);
            else
            if (keycode == 'home')
                offset = 0;
            else
            if (keycode == 'end') {
                fseek(fd, 0, 2);
                offset = ftell(fd);
            }

            if (offset < 0)
                offset = 0;

            fseek(fd, 0, 2);
            if (offset >= ftell(fd))
                offset = ftell(fd);

            return hex;
        },
        render:     sub (hex) {
            local out = [];
            local o;

            if (tx != mp.window.tx) {
                /* calculate bytes per line */
                bpl = 100;

                while ((12 + (bpl * 4)) > mp.window.tx)
                    bpl -= 1;

                tx = mp.window.tx;
            }

            if (h_cursor > bpl)
                h_cursor = bpl - 1;

            /* round offset */
            offset -= offset % bpl;

            fseek(fd, offset, 0);

            o = offset;

            while (size(out) < mp.window.ty) {
                local ov, nv, av, n, c;

                nv = [];
                av = [
                    mp.colors.word2.attr,
                    sprintf(' %s ', mp.unicode("vert"))
                ];
                ov = [
                    mp.colors.word1.attr,
                    sprintf(" %06X %s", ftell(fd), mp.unicode("vert"))
                ];

                for (n = 0; n < bpl; n += 1) {
                    local a = 0;

                    if ((c = getchar(fd)) == NULL) {
                        n -= 1;
                        break;
                    }

                    oc = ord(c);

                    if (h_cursor == n)
                        a = mp.colors.cursor.attr;
                    else
                    if (o >= f_offset && o < f_offset + f_size)
                        a = mp.colors.search.attr;
                    else
                    if (oc == 0)
                        a = mp.colors.word2.attr;
                    else
                    if (oc < 32)
                        a = mp.colors.word3.attr;
                    else
                    if (oc >= ord(' ') && oc <= 126)
                        a = mp.colors.quotes.attr;
                    else
                        a = mp.colors.normal.attr;

                    push(nv, mp.colors.normal.attr, " ",
                        a, sprintf("%02X", ord(c)));

                    if (oc == 0x0a)
                        c = mp.unicode("pilcrow");
                    else
                    if (oc < 32) {
                        if (mp.config.use_unicode)
                            c = chr(oc + 0x2400);
                        else
                            c = mp.unicode("middledot");
                    }

                    if (oc > 126)
                        c = mp.unicode("middledot");

                    push(av, a, c);

                    o += 1;
                }

                while (n < bpl) {
                    push(nv, mp.colors.normal.attr, '   ');
                    push(av, mp.colors.word2.attr,  ' ');
                    n += 1;
                }

                push(out, ov->join(nv)->join(av));

                if (c == NULL)
                    break;
            }

            while (size(out) < mp.window.ty)
                push(out, []);

            return out;
        },
        actions:    {
            seek_next:  sub (hex) {
                fseek(hex.fd, hex.f_offset + 1);

                mp.busy(1);
                local found = mp_c.search_hex(hex.fd, hex.data);
                mp.busy(0);

                if (found) {
                    hex.f_offset  = ftell(hex.fd);
                    hex.f_size    = hex.data->size() / 2;
                    hex.offset    = hex.f_offset;
                }
                else
                    mp.alert(L("Not found."));
            },

            seek:       sub (hex) {
                local t = mp.form(
                    [
                        {
                            label:   L("Hex string to seek") + ':',
                            type:    'text',
                            history: 'search-hex'
                        }
                    ]
                );

                if (t != NULL) {
                    hex.data = t[0];
                    hex.f_offset = hex.offset - 1;
                    hex->actions.seek_next();
                }
            },

            go_to_offset: sub (hex) {
                local t = mp.form([{
                    label:   L("Address") + ":",
                    type:    "text",
                    history: "go_to_offset"}]);

                if (t != NULL) {
                    hex.offset = integer(t[0]);

                    if (hex.bpl > 0)
                        hex.h_cursor = hex.offset % hex.bpl;
                }
            }
        },
        keycodes:   {
            "ctrl-f"    => 'seek',
            "f3"        => 'seek_next',
            "ctrl-g"    => 'go_to_offset'
        }
    }
);
